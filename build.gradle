plugins {
    id 'me.champeau.gradle.jmh' version '0.3.1'
    id 'java'
    id 'groovy'
}
group 'pl.polsl'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.h2database', name: 'h2', version: '1.4.192'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.6'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'

    compile group: 'junit', name: 'junit', version: '4.12'
    compile "org.openjdk.jmh:jmh-core:1.13"
    compile "org.openjdk.jmh:jmh-generator-bytecode:1.13"

    compile group: 'mysql', name: 'mysql-connector-java', version: '6.0.6'
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.4'

}


boolean development = false

jmh {
    jmhVersion = '1.13'
    
    // change memory here, DEFAULT_JVM_OPTS doesn't work for some reason
    jvmArgs = '-Xms8192m -Xmx8192m'

    // Use ./gradlew -Pinclude='regex' to run specific class
    include = project.hasProperty('include') ? project.getProperty('include') : ''

    if (development) {
        threads = 1
        fork = 0
        warmupIterations = 0
        iterations = 5
    } else {
        threads = 1
        fork = 1
        warmupIterations = 1
        iterations = 5
    }
    // https://github.com/melix/jmh-gradle-plugin/issues/79
    duplicateClassesStrategy 'warn'
}

sourceSets {
    test {
        java {
            srcDirs += ['src/jmh/java']
        }
    }
}

// http://mrhaki.blogspot.com/2014/10/gradle-goodness-show-standard-out-or.html
test {
    testLogging {
        showStandardStreams = true
    }
}

task memoryUsage(type: JavaExec, dependsOn: 'compileJava') {
    main = 'mastersthesis.MemoryUsageTpc'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ['-Xms8192m', '-Xmx8192m']
    args (
            project.hasProperty('size') ? project.getProperty('size') : '',
            project.hasProperty('type') ? project.getProperty('type') : ''
    )
}
